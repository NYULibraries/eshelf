# Gather the IDs for the various collection controls
gather_ids = (event) ->
  # Get the checked IDs
  checked_ids = $("input[name='id[]']:checked")
  if checked_ids.length is 0
    $("#select_something").show()
    # http://stackoverflow.com/questions/1357118/event-preventdefault-vs-return-false
    return false
  $("#select_something").hide()
  # Add question mark if necessary
  unless event.target.href.match '\\?'
    event.target.href += "?"
  # Add an ampersand if necessary
  unless event.target.href.match '\\?$' or event.target.href.match '&$'
    event.target.href += "&"
  # Get the expired data
  expired_data = $(event.target).attr("data-expired")
  # Remove the expired data, if there is any
  if expired_data
    event.target.href = event.target.href.replace(expired_data, "")
  # Append the checkedIDs to the URL.
  external_ids = $("input[name='id[]']:checked").map( () ->
    $(this).data("external-id")
  ).get()
  external_ids = "external_id[]=" + external_ids.join("&external_id[]=")
  if event.target.id in ["refworks", "endnote", "ris", "bibtex"]
    event.target.href += external_ids
    # Expire this data
    $(event.target).attr("data-expired", external_ids)
  else
    event.target.href += $.param(checked_ids)
    # Expire this data
    $(event.target).attr("data-expired", $.param(checked_ids))

toggle_all_input = (event) ->
  if $(event.target).is(":not(:checked)")
    select_all()
    true
  else
    select_none()
    true

toggle_all = (event) ->
  if $(event.target).find('input#select').is(":not(:checked)")
    select_all()
    true
  else
    select_none()
    true

select_all = (event)->
  $("input[name='id[]']").map( () ->
    $(this).prop('checked',true)
  )
  $($("input#select:not(:checked)")).prop('checked', true)
  false

select_none = (event)->
  $("input[name='id[]']").map( () ->
    $(this).removeAttr('checked')
  )
  $($("input#select:checked")).removeAttr('checked')
  false

$ ->
  # The controls that we are enabling...
  collection_controls =
    ["delete", "refworks", "endnote", "ris", "bibtex"]
  # ...need to gather IDs on click.
  $("##{collection_controls.join(",#")}").click(gather_ids)
  $(".email").click(gather_ids)
  $(".print").click(gather_ids)
  $("input[name='id[]']").click (event) -> $("#select_something").hide()
  $("input#select").closest("button").click (event) -> toggle_all(event)
  $("input#select").click (event) -> toggle_all_input(event)
  $(".select-all").click (event) -> select_all(event)
  $(".select-none").click (event) -> select_none(event)
